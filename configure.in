# Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.21 2003/03/11 03:40:27 dnelson Exp $
AC_INIT(spamass-milter, 0.1.3a+cvs)
AC_PREREQ(2.53)
AC_REVISION($Revision: 1.21 $)
AC_CONFIG_SRCDIR([spamass-milter.cpp])

# Initialize automake now
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC

# Search for spamc
dnl AC_ARG_VAR(SPAMC,[Spamc command])
AC_PATH_PROGS(SPAMC, spamc)
if test "x$SPAMC" = x ; then
   AC_MSG_ERROR([Spamc not found. Is SpamAssassin installed?])
fi
AC_DEFINE_UNQUOTED(SPAMC, "$SPAMC", [Spamc command])

# Search for pthreads, and make them the default (have to do it here else
# the -lmilter check will fail to link)
ACX_PTHREAD(,AC_MSG_ERROR([Cannot determine how to use pthreads]))
CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
CXXFLAGS="$CXXFLAGS $PTHREAD_CFLAGS"
LIBS="$LIBS $PTHREAD_LIBS"

# Checks for header files.
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([fcntl.h syslog.h sys/select.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T

CF_GNU_SOURCE

AC_ARG_ENABLE(emulate-poll, AC_HELP_STRING([--enable-emulate-poll],[use a select wrapper instead of poll (autodetected)]))

# If the system doesn't have poll.h, or if the user forced it, provide
# our own poll function
if test x$enable_emulate_poll != xyes ; then
AC_CHECK_HEADERS(poll.h)
fi

if test x$enable_emulate_poll = xyes -o x$ac_cv_header_poll_h = xno ; then
AC_MSG_NOTICE([Emulating poll with select])
AC_LIBOBJ(poll)
fi

# Checks for library functions.
AC_CHECK_FUNCS([vsyslog vasprintf vsnprintf])
AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(connect, socket)
AC_SEARCH_LIBS(inet_aton, resolv)

# Check for libmilter and its header files in the usual locations
if test -d /usr/lib/libmilter ; then
	LDFLAGS="$LDFLAGS -L/usr/lib/libmilter"
fi
AC_CHECK_LIB(milter, mi_stop,,[
	# Older sendmails require libsm for support functions
	AC_SEARCH_LIBS(strlcpy, sm smutil)
	$as_unset ac_cv_lib_milter_mi_stop
	AC_CHECK_LIB(milter, mi_stop,,[
		AC_MSG_ERROR([Cannot find libmilter])
	])
])
AC_CHECK_HEADERS(libmilter/mfapi.h,,[
	AC_MSG_ERROR([Please install mfapi.h from the sendmail distribution])
])

# Makefiles to create:
AC_OUTPUT(Makefile contrib/spamass-milter.spec spamass-milter.1)
