# Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.8 2002/07/26 04:32:59 dnelson Exp $
AC_INIT(spamass-milter, 0.1.2)
AC_PREREQ(2.53)
AC_REVISION($Revision: 1.8 $)
AC_CONFIG_SRCDIR([spamass-milter.cpp])

# Initialize automake now
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

# Checks for programs.
AC_PROG_CXX
AC_PROG_CC

# Search for spamc
AC_PATH_PROG(SPAMC, spamc)
if test "x$SPAMC" = x ; then
   AC_MSG_ERROR([Spamc not found. Is SpamAssassin installed?])
fi
AC_DEFINE_UNQUOTED(SPAMC, "$SPAMC", [location of spamc])

# Search for pthreads, and make them the default (have to do it here else
# the -lmilter check will fail to link)
ACX_PTHREAD(,AC_MSG_ERROR([Cannot determine how to use pthreads]))
CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
CXXFLAGS="$CXXFLAGS $PTHREAD_CFLAGS"
LIBS="$LIBS $PTHREAD_LIBS"

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T

CF_GNU_SOURCE

# Checks for library functions.
AC_CHECK_FUNCS([vsyslog vasprintf vsnprintf])
AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(connect, socket)
AC_SEARCH_LIBS(inet_aton, resolv)

# Check for libmilter and its header files in the usual locations
LDFLAGS="$LDFLAGS -L/usr/lib/libmilter"
AC_CHECK_LIB(milter, mi_stop,,[
	# Older sendmails require libsm for support functions
	AC_SEARCH_LIBS(strlcpy, sm smutil)
	$as_unset ac_cv_lib_milter_mi_stop
	AC_CHECK_LIB(milter, mi_stop,,[
		AC_MSG_ERROR([Cannot find libmilter])
	])
])
AC_CHECK_HEADERS(libmilter/mfapi.h,,[
	AC_MSG_ERROR([Please install mfapi.h from the sendmail distribution])
])

# Makefiles to create:
AC_OUTPUT(Makefile contrib/spamass-milter.spec spamass-milter.1)
